allprojects {
	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'eclipse'

	def defaultEncoding = 'UTF-8'
	[compileJava, compileTestJava, javadoc]*.options*.encoding = defaultEncoding

	version = '1.0'
	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		mavenCentral()
		jcenter()
		maven { url 'http://repo.spring.io/release' }
		maven { url 'http://repo.spring.io/snapshot' }
		maven { url 'http://repo.spring.io/milestone' }
		maven { url 'http://maven.seasar.org/maven2/' }
		maven { url "file:${System.getProperty('user.home')}/.m2/repository" }
	}

	test {
		systemProperties 'property': 'value'
	}
}

buildscript {
	repositories {
		jcenter()
		maven { url 'http://repo.spring.io/snapshot' }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.0.BUILD-SNAPSHOT")
	}
}

apply plugin: 'war'
apply plugin: 'spring-boot' // http://docs.spring.io/spring-boot/docs/current/reference/html/build-tool-plugins-gradle-plugin.html
apply plugin: 'pmd'
apply plugin: 'jacoco'

def SPRING_GROUPID = 'org.springframework'
def SPRING_BOOT_GROUP_ID = 'org.springframework.boot'
def SPRING_VERSION = '4.1.6.RELEASE'
def SPRING_BOOT_VERSION = '1.3.0.BUILD-SNAPSHOT'
def SPRING_SECURITY_VERSION = '4.0.1.RELEASE'
def ASPECTJ_VERSION = '1.8.6'
def LOGBACK_VERSION = '1.1.3'
dependencies {

	// Settings providedCompile
	providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
	// Settings Base
	compile (group: SPRING_BOOT_GROUP_ID, name: 'spring-boot-starter-web', version: SPRING_BOOT_VERSION) {
//		exclude module: 'hibernate-validator'
	}
	compile group: SPRING_BOOT_GROUP_ID, name: 'spring-boot-devtools', version: SPRING_BOOT_VERSION
//	compile group: SPRING_GROUPID, name: 'spring-context', version: SPRING_VERSION
//	compile group: SPRING_GROUPID, name: 'spring-web', version: SPRING_VERSION
//	compile group: SPRING_GROUPID, name: 'spring-webmvc', version: SPRING_VERSION
//	compile group: 'org.springframework.security', name: 'spring-security-core', version: SPRING_SECURITY_VERSION
//	compile group: 'org.springframework.security', name: 'spring-security-web', version: SPRING_SECURITY_VERSION
//	compile group: 'org.springframework.security', name: 'spring-security-config', version: SPRING_SECURITY_VERSION
	compile group: 'org.thymeleaf', name: 'thymeleaf-spring4', version: '2.1.4.RELEASE'
	compile group: 'nz.net.ultraq.thymeleaf', name: 'thymeleaf-layout-dialect', version: '1.2.9'
	compile group: 'org.projectlombok', name: 'lombok', version: '1.16.4'
	// Settings Database
	compile group: SPRING_GROUPID, name: 'spring-jdbc', version: SPRING_VERSION
	compile group: SPRING_GROUPID, name: 'spring-tx', version: SPRING_VERSION
	compile group: 'org.apache.tomcat', name: 'tomcat-jdbc', version: '8.0.23'
	compile group: 'org.dbflute', name: 'dbflute-runtime', version: '1.1.0-sp5'
	compile group: 'com.h2database', name: 'h2', version: '1.3.176'
	// Settings Logging
//	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.11'
	compile group: 'ch.qos.logback', name: 'logback-classic', version: LOGBACK_VERSION
	compile group: 'ch.qos.logback', name: 'logback-core', version: LOGBACK_VERSION
	// Settings AOP
	compile group: 'org.aspectj', name: 'aspectjrt', version: ASPECTJ_VERSION
	compile group: 'org.aspectj', name: 'aspectjweaver', version: ASPECTJ_VERSION
	// Settings Other
	compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5'
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.6.0-rc2'
	compile group: 'org.seasar.util', name: 's2util', version: '0.0.1'
	compile group: 'commons-fileupload', name: 'commons-fileupload', version: '1.3.1'
	compile (group: 'org.eclipse.birt.runtime', name: 'org.eclipse.birt.runtime', version: '4.4.2') {
		exclude module: 'org.eclipse.datatools.connectivity.apache.derby.dbdefinition'
		exclude module: 'org.eclipse.datatools.connectivity.apache.derby'
		exclude module: 'org.eclipse.datatools.connectivity.console.profile'
		exclude module: 'org.eclipse.datatools.connectivity.db.generic'
		exclude module: 'org.eclipse.datatools.connectivity.dbdefinition.genericJDBC'
		exclude module: 'org.eclipse.datatools.connectivity.oda.consumer'
		exclude module: 'org.eclipse.datatools.connectivity.oda.design'
		exclude module: 'org.eclipse.datatools.connectivity.oda.flatfile'
		exclude module: 'org.eclipse.datatools.connectivity.oda.profile'
		exclude module: 'org.eclipse.datatools.connectivity.sqm.core'
		exclude module: 'org.eclipse.datatools.connectivity'
		exclude module: 'org.eclipse.datatools.modelbase.dbdefinition'
		exclude module: 'org.eclipse.datatools.modelbase.derby'
		exclude module: 'org.eclipse.datatools.modelbase.sql.query'
		exclude module: 'org.eclipse.datatools.modelbase.sql'
		exclude module: 'org.eclipse.datatools.enablement.hsqldb.dbdefinition'
		exclude module: 'org.eclipse.datatools.enablement.hsqldb'
		exclude module: 'org.eclipse.datatools.enablement.oracle.dbdefinition'
		exclude module: 'org.eclipse.datatools.enablement.oracle'
		exclude module: 'org.eclipse.datatools.enablement.ibm.db2.luw.dbdefinition'
		exclude module: 'org.eclipse.datatools.enablement.ibm.db2.luw'
		exclude module: 'org.eclipse.datatools.enablement.ibm.informix.dbdefinition'
		exclude module: 'org.eclipse.datatools.enablement.ibm.informix'
		exclude module: 'org.eclipse.datatools.enablement.msft.sqlserver.dbdefinition'
		exclude module: 'org.eclipse.datatools.enablement.msft.sqlserver'
		exclude module: 'org.eclipse.datatools.enablement.mysql.dbdefinition'
		exclude module: 'org.eclipse.datatools.enablement.mysql'
		exclude module: 'org.eclipse.datatools.enablement.oda.ws'
		exclude module: 'org.eclipse.datatools.enablement.oda.xml'
		exclude module: 'org.eclipse.datatools.enablement.postgresql.dbdefinition'
		exclude module: 'org.eclipse.datatools.enablement.postgresql'
		exclude module: 'org.eclipse.orbit.mongodb'
	}

	// Settings Local
	compile group: 'com.github.hatimiti', name: 'flutist-common', version: '1.0'

	// Settings Test
	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile group: SPRING_GROUPID, name: 'spring-test', version: SPRING_VERSION
	testCompile group: SPRING_BOOT_GROUP_ID, name: 'spring-boot-starter-test', version: SPRING_BOOT_VERSION
	testCompile group: 'org.dbunit', name: 'dbunit', version: '2.5.1'

    providedRuntime group: SPRING_BOOT_GROUP_ID, name: 'spring-boot-starter-tomcat', version: SPRING_BOOT_VERSION
}

jar {
	manifest {
		attributes 'Implementation-Title': 'Gradle Quickstart', 'Implementation-Version': version
	}
}

war {
	baseName = 'doxer'
	version = '0.1'
}

configurations {
	providedRuntime
}

bootRepackage {
//	enabled = false
	mainClass = 'org.doxer.Application'
}

uploadArchives {
	repositories {
		flatDir {
			dirs 'repos'
		}
	}
}

pmd { // コーディングチェック
	toolVersion = '5.1.2'
	ignoreFailures = true
	sourceSets = [sourceSets.main]
	reportsDir = file("$project.buildDir/reports/pmd")
	ruleSets = [
		'java-basic',
		'java-strings',
		'java-braces'
	]
}
pmdMain {
	excludes = [
		'**/org/seasar/**/*.java',
		'**/db/dbflute/allcommon/**/*.java',
		'**/db/dbflute/bsbhv/**/*.java',
		'**/db/dbflute/bsentity/**/*.java',
		'**/db/dbflute/cbean/bs/**/*.java',
		'**/db/dbflute/cbean/cq/bs/**/*.java',
		'**/db/dbflute/cbean/cq/ciq/**/*.java',
	]
}

jacoco { // コードカバレッジ測定
	toolVersion = "0.7.5.201505241946"
	reportsDir = file("$project.buildDir/reports/jacoco/")
}

jacocoTestReport {
	reports {
		csv.enabled true
		html.destination "$project.buildDir/reports/jacoco/html/"
	}
}

test {
	systemProperty 'spring.profiles.active', 'ut'

	jacoco {
		append = false
		destinationFile = file("$buildDir/reports/jacoco/test.exec")
		classDumpFile = file("$buildDir/reports/jacoco/classpathdumps")
	}
}

check << { // CPD: 重複コードチェック
	File outDir = file("$project.buildDir/reports/pmd/")
	outDir.mkdirs()
	ant.taskdef(name: 'cpd', classname: 'net.sourceforge.pmd.cpd.CPDTask',
		classpath: configurations.pmd.asPath)
	ant.cpd(minimumTokenCount: '100', format: 'csv',
		outputFile: new File(outDir, 'cpd.csv'))
	{
		fileset(dir: 'src/main/java') {
			include(name: '**/*.java')
			exclude(name: '**/db/dbflute/allcommon/**/*.java')
			exclude(name: '**/db/dbflute/bsbhv/**/*.java')
			exclude(name: '**/db/dbflute/bsentity/**/*.java')
			exclude(name: '**/db/dbflute/cbean/bs/**/*.java')
			exclude(name: '**/db/dbflute/cbean/cq/bs/**/*.java')
			exclude(name: '**/db/dbflute/cbean/cq/ciq/**/*.java')
		}
	}
}

/**
 * テストクラスのJavaDocを生成します．
 * (試験仕様書として利用)
 */
task javadoctest(type: Javadoc) {
	[javadoctest]*.options*.encoding = 'UTF-8'
	docsDirName = "docs/test/";
	source 'src/test/java'
}
