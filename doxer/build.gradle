apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'pmd'
apply plugin: 'jacoco'

def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.encoding = defaultEncoding

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = '1.0'
jar {
	manifest {
		attributes 'Implementation-Title': 'Gradle Quickstart', 'Implementation-Version': version
	}
}

war {
	webAppDirName = 'WebContent'
}

repositories {
	mavenCentral()
	maven { url "http://repo.spring.io/release" }
}

def SPRING_GROUPID = 'org.springframework'
def SPRING_VERSION = '4.1.4.RELEASE'
def ASPECTJ_VERSION = '1.8.2'
def LOGBACK_VERSION = '1.1.2'
dependencies {
	// Settings providedCompile
	providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
	// Settings Base 
	compile group: SPRING_GROUPID, name: 'spring-context', version: SPRING_VERSION
	compile group: SPRING_GROUPID, name: 'spring-web', version: SPRING_VERSION
	compile group: SPRING_GROUPID, name: 'spring-webmvc', version: SPRING_VERSION
	compile group: 'org.thymeleaf', name: 'thymeleaf-spring4', version: '2.1.4.RELEASE'
	compile group: 'org.projectlombok', name: 'lombok', version: '1.16.0'
	// Settings Database
	compile group: SPRING_GROUPID, name: 'spring-jdbc', version: SPRING_VERSION
	compile group: SPRING_GROUPID, name: 'spring-tx', version: SPRING_VERSION
	compile group: 'org.apache.tomcat', name: 'tomcat-jdbc', version: '8.0.18'
	compile group: 'org.dbflute', name: 'dbflute-runtime', version: '1.1.0-sp1'
	compile group: 'com.h2database', name: 'h2', version: '1.4.185'
	// Settings Logging
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.10'
	compile group: 'ch.qos.logback', name: 'logback-classic', version: LOGBACK_VERSION
	compile group: 'ch.qos.logback', name: 'logback-core', version: LOGBACK_VERSION
	// Settings AOP
	compile group: 'org.aspectj', name: 'aspectjrt', version: ASPECTJ_VERSION
	compile group: 'org.aspectj', name: 'aspectjweaver', version: ASPECTJ_VERSION
	// Settings Other
	compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.4'
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.5.1'
	// Settings Test
	testCompile group: 'junit', name: 'junit', version: '4.+'
	testCompile group: SPRING_GROUPID, name: 'spring-test', version: SPRING_VERSION
}

test {
	systemProperties 'property': 'value'
}

uploadArchives {
	repositories {
	   flatDir {
		   dirs 'repos'
	   }
	}
}

pmd {
	ignoreFailures = true
	sourceSets = [sourceSets.main]
	reportsDir = file("$project.buildDir/reports/pmd")
	ruleSets = [
		'java-basic',
		'java-strings',
		'java-braces'
	]
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}

check << {
	File outDir = new File('build/reports/pmd/')
	outDir.mkdirs()
	ant.taskdef(name: 'cpd', classname: 'net.sourceforge.pmd.cpd.CPDTask',
		classpath: configurations.pmd.asPath)
	ant.cpd(minimumTokenCount: '100', format: 'xml',
		outputFile: new File(outDir, 'cpd.xml'))
	{
		fileset(dir: 'src/main/java') {
			include(name: '**/*.java')
		}
	}
}
